plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.google.cloud.tools.jib' version '3.4.2'
    id "org.asciidoctor.jvm.convert" version '3.3.2'
}

group = 'com.flip'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    asciidoctorExt

    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    implementation 'com.auth0:java-jwt:4.0.0'
    // DB 형상관리  
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //Querydsl 추가
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Spring Rest Docs
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

jib {
    from {
        image = "openjdk:17-alpine"
    }
    to {
        image = System.getenv("DOCKERHUB_REPO") ?: ""
        tags = [System.getenv("SHORT_SHA")]
    }

    container {
        jvmFlags = ["-Duser.timezone=Asia/Seoul"]
        environment = [
                "SPRING_PROFILES_ACTIVE": System.getenv("SPRING_PROFILES_ACTIVE"),
                "DATASOURCE_URL"        : System.getenv("DATASOURCE_URL"),
                "DATASOURCE_USERNAME"   : System.getenv("DATASOURCE_USERNAME"),
                "DATASOURCE_PASSWORD"   : System.getenv("DATASOURCE_PASSWORD"),
                "TZ"                    : "Asia/Seoul"
        ]
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

def testProfile = project.hasProperty("testProfile") ? project.property("testProfile").toString() : "test-local"

test {
    systemProperty("spring.profiles.active", testProfile)
    outputs.dir snippetsDir
    useJUnitPlatform()
}

clean {
    delete file('src/main/generated')
}

asciidoctor {
    dependsOn test
    baseDirFollowsSourceFile()
    sources {
        include("**/index.adoc")
    }
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
}

// build/docs/asciidoc 디렉토리에 있는 파일을 src/main/resources/static/docs 디렉토리로 복사
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}
